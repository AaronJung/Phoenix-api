import org.gradle.internal.os.OperatingSystem
import java.nio.file.Files
import java.nio.file.Path
import java.nio.file.Paths

plugins {
  id 'cpp'
  id 'java'
  id 'edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin' version '2.1'
  id 'edu.wpi.first.NativeUtils' version '1.7.0'
  id 'edu.wpi.first.GradleVsCode' version '0.3.0'
  id 'google-test-test-suite'
}

apply from: "locations.gradle"
apply from: "options.gradle"

apply from: 'config.gradle'

def everythingExceptAthena = ['linux:x86-64', 'linux:armhf', 'linux:aarch64', 'windows:x86-64', 'windows:x86', 'osx:x86-64']

ext.sharedFrcConfigs = [CTRE_Phoenix: ['linux:athena']]
ext.sharedConfigs = [CTRE_Phoenix: [], DummyLinkForJavaTest: ['linux:x86-64', 'linux:armhf', 'linux:aarch64', 'windows:x86-64', 'windows:x86', 'osx:x86-64']]

//For googletest
ext.staticTestConfigs = [CTRE_PhoenixTest: []]

//For java unit test linking
ext.sharedTestConfigs = [DummyLinkForJavaTest: ['linux:x86-64', 'linux:armhf', 'linux:aarch64', 'windows:x86-64', 'windows:x86', 'osx:x86-64']]

ext.javaLibsFiles = []
ext.javaLibsDir = []

apply from: 'dependencies.gradle'

model {
  exportsConfigs {
      CTRE_Phoenix(ExportsConfig) {
          x86ExcludeSymbols = ['_CT??_R0?AV_System_error', '_CT??_R0?AVexception', '_CT??_R0?AVfailure',
                                '_CT??_R0?AVbad_cast',
                                '_CT??_R0?AVruntime_error', '_CT??_R0?AVsystem_error', '_CTA5?AVfailure',
                                '_TI5?AVfailure']
          x64ExcludeSymbols = ['_CT??_R0?AV_System_error', '_CT??_R0?AVexception', '_CT??_R0?AVfailure',
                                '_CT??_R0?AVbad_cast',
                                '_CT??_R0?AVruntime_error', '_CT??_R0?AVsystem_error', '_CTA5?AVfailure',
                                '_TI5?AVfailure']
      }
  }
  components {
    CTRE_Phoenix(NativeLibrarySpec) {
      sources {
        cpp {
          source {
            srcDirs 'src/main/native/cpp'
            include '**/*.cpp'
          }
          exportedHeaders {
            srcDirs 'src/main/native/include'
          }
        }
      }
    }
    DummyLinkForJavaTest(NativeLibrarySpec) {
      sources {
        cpp {
          source {
            srcDirs 'src/dummy/native/cpp'
            include '**/*.cpp'
          }
          exportedHeaders {
            srcDirs 'src/dummy/native/include'
          }
        }
      }
    }
  }
  testSuites {
    CTRE_PhoenixTest(GoogleTestTestSuiteSpec) {
      for(NativeComponentSpec c : $.components) {
        if (c.name == 'CTRE_Phoenix') {
          testing c
          break
        }
      }
      sources {
        cpp {
          source {
            srcDirs 'src/test/native/cpp'
            include '**/*.cpp'
          }
          exportedHeaders {
            srcDirs 'src/test/native/include', 'src/main/native/include'
          }
        }
      }
    }
  }
  binaries {
    //Only build a test suite for native execution (for now at least)
    withType(GoogleTestTestSuiteBinarySpec) {
      if (it.targetPlatform.architecture.name != 'x86' && it.targetPlatform.architecture.name != 'x86-64') {
        it.buildable = false
        return
      }    

      lib library: 'CTRE_Phoenix', linkage: 'static'
    }
    //If we are not building for windows, don't bother with the shared library
    withType(StaticLibraryBinarySpec) {
      if(it.component.name == 'DummyLinkForJavaTest') {
        it.buildable = false
      }
    }
    withType(SharedLibraryBinarySpec) {
      if(it.component.name == 'DummyLinkForJavaTest') {
        if (it.targetPlatform.architecture.name == 'x86' || it.targetPlatform.architecture.name == 'x86-64') {
          for(lib in it.libs) {
            if(lib.linkFiles.asFileTrees.size() > 0) {
              javaLibsDir << lib.linkFiles.asFileTrees.get(0).dir
              javaLibsFiles << lib.linkFiles.asFileTrees.get(0).file
            }  
          }
        }
      }
      else if (it.targetPlatform.operatingSystem.name != 'windows') {
        it.buildable = false
        return
      }    
    }
  }
}

//task renamePlatformForJavaLink(type: Copy) {
//    def cpDir = ''
//    for(dir in javaLibsDir) {
//        println dir
//        if(dir.toString().contains('platform')) {
//        
//        }
//    }
//    from dir
//    into dir
//    include 'libCTRE_PhoenixPlatform_sim.so'
//    rename 'libCTRE_PhoenixPlatform_sim.so', 'libCTRE_PhoenixPlatform.so'
//}


task linkJava {
    doLast {
        for(dir in javaLibsDir) {
            if(dir.toString().contains('cci')) {
                test.jvmArgs = ['-Djava.library.path=' + dir.toString()]
            }
            if(dir.toString().contains('platform')) {
                Path source = Paths.get(dir.toString() + java.io.File.separator + 'libCTRE_PhoenixPlatform_sim.so')           
                Path target = Paths.get(dir.toString() + java.io.File.separator + 'libCTRE_PhoenixPlatform.so')
                Files.copy(source, target)     
            }
        }
        if(OperatingSystem.current().isWindows()) {
            javaLibsDir << test.environment.get('PATH')
            test.environment.put('PATH', javaLibsDir.join(';'))
        }
        else if(OperatingSystem.current().isMacOsX()) { 
            test.environment.put('DYLD_LIBRARY_PATH', javaLibsDir.join(';'))
        }
        else if(OperatingSystem.current().isLinux()) { 
            test.environment.put('LD_LIBRARY_PATH', javaLibsDir.join(';'))
        } 
    }
}

test.dependsOn linkJava
linkJava.dependsOn assemble


apply from: 'publish.gradle'

wrapper {
  gradleVersion = '4.9'
}
